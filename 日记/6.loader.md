# loader 加载时加载程序需要先写进硬盘
```c
dd if=loader.bin of=master.img bs=512 count=3 seek=2 conv=notrunc//代表2号扇区开始，写3个扇区，每个扇区512字节
```

# 1.loader进行内存检测：

## 内存检测可以使用bios提供的0x15中断来获取内存信息,也可以使用cpu直接读取内存，来判断内存的大小，是否可读可写。
--------------------
### 使用bios中断需要提供一些参数：

```c
es:di代表内存检测的结果写入内存的地址

ebx需要初始化为0

eax需要设置成0xe820

ecx设置结构体的长度最低为20个字节

edx标志位设置成0x534d4150
```
### 启用0x15中断
```c
int 0x15
```
### 返回值：
```c
ebx中的值，观察是否为0（在进行内存检测时bios会改变ebx中的值，只要不为0，就一直进行检测
cf如果cf位为1则内存检测错误需要进行相应处理
返回值为20个字节，8位表示基地址，8位表示长度，4位表示内存类型

es:di位不变

eax变为0x534d4150

ecx值不变
```

代码如下：
```c
check_momery:
    mov ax,0
    mov es,ax
    mov di,save_struct;设置结构体所写的位置
    mov ebx,0


    
    .loop_check :
    mov eax,0xe820;设置标志参数

    mov ecx,20;设置内存范围结构体的数据宽度为20位字节

    mov edx, 0x534d4150;设置标志位

    int 0x15;启动中断

    jc error;错误
    add di,20

    cmp ebx,0
    jnz  .loop_check

    ret 
show :
    mov eax, [save_struct]
    mov ebx, [save_struct+8]
    ret 

    

```
-----------------------------------------
------------------------------------------
# 2.loader设置gdt
```c
load_gdt:
    base equ 0
    code_base equ (1<<3)   ;设置代码段0000_0000_0000_1_000  偏移地址为1
    data_base equ (2<<3)   ;设置数据段0000_0000_0001_0_000  偏移地址为2

    cli
    in al,0x92 
    or al,0b10  ;将A20总线的第二位设置为1以启用A20总线
    out 0x92,al



    lgdt [gdt_place] ;加载gdt
    
    mov eax,cr0
    or eax,1
    mov cr0,eax    ;将CR0的最后一位置1进入保护模式

    jmp dword code_base:load_ok  ;清楚cpu缓存




gdt_place:    ;存放gdt表地址和大小
dw (gdt_end - gdt_base) - 1     ;存放gdt大小
dd gdt_base                     ;存放gdt起始地址
gdt_base:
    dd 0,0                      ;首个描述符需要设置成0
    .gdt_code:                  ;代码段
    dw 0xffff                   ;代码段大小低16位
    dw base                     ;代码段基地址0-15
    db base>>16                 ;16-24
    db 0b1_00_1_1010            ;在内存中__特权级00__数据段/代码段__代码段x___不依从___可读___没有访问过
    db 0b1_1_0_1_1111           ;颗粒度为4k__32位__不进行64位扩展__固定1__代码段大小高4位
    db base>>24                 ;代码段基地址25-32

    .gdt_data:                  ;数据段
    dw 0xffff                   ;数据段大小0-15位
    dw base                     ;数据段基地址0-15位
    db base>>16                 ;数据段基地址16-14位
    db 0b1_00_1_0010            ;在内存中_特权级00_数据段/数据段__数据段x__向上扩展__可写__没有被访问过
    db 0b1_1_0_0_1111           ;颗粒度为4k__32位__不进行64位扩展__固定位1__数据段大小高4位
    db base>>24
gdt_end:

   
    
[bits 32]   ;进入32位
;加载完成进行检查
load_ok :
    mov ax,data_base  ;设置ds,es,ss,f,gs为数据段
    mov es,ax
    mov ds,ax
    mov fs,ax
    mov ss,ax
    mov esp,0x10000

    ;进入保护模式后便不能调用bios中断   
    mov byte [0xb8000],'p'      ;b8000为屏幕的第一个像素
    jmp $
 

```

# loader加载内核并进入内核
首先需要从磁盘上将内核加载到内存的0x10000中，之后跳转到内核
```c
    mov edi, 0x10000
    mov cx, 200 ;扇区数量
    mov ebx, 10 ;扇区起始地址
    call read_disk
    jmp code_base:0x10000
```

其中read_disk和boot中的一样

# 调整目录结构并更新Makefile文件：

source-

    |-boot(引导程序)
    |-kneral(内核文件)
    |-src(操作台)
    |-build(存放临时文件)


更改后的Makefile文件：
```Makefile
build := ../build
boot := ../boot
kneral := ../kneral
entrypoit :=0x10000 #内核的起始地址

$(build)/boot/%.bin : $(boot)/%.asm
	mkdir -p $(build)/boot/

	nasm -f bin $< -o $@

$(build)/kneral/%.o : $(kneral)/%.asm
	mkdir -p $(build)/kneral/
	nasm -f elf32 $< -o $@ #将内核文件编译成重定位文件  （-f elf 设置输出文件为32位elf文件，$<为依赖文件，$@为输出文件）

$(build)/kneral/kneral.bin : $(build)/kneral/kneral.o

	ld -m elf_i386 -static $^ -o $@ -Ttext $(entrypoit) #将内核与其他文件链接起来生成可执行文件并指定起始地点

$(build)/system.bin : $(build)/kneral/kneral.bin

	objcopy -O binary $< $@ #将bss未初始化数据展开 （bss简介：如c语言int a[10]；和int a[]=10；第一种a只声明未定义因此为bss段，第二句既声明又定义因此放在data段,放在bss段的时候不会写在10个int字节里填充0，而是会记录0的个数）



$(build)/master.img : $(build)/boot/boot.bin \
	$(build)/boot/loader.bin \
	$(build)/kneral/kneral.bin \
	$(build)/system.bin \
#换行用‘\’表示
	yes | bximage -q -hd=16 -func=create -sectsize=512 -imgmode=flat $(build)/master.img
	dd if=$(build)/boot/boot.bin of=$(build)/master.img bs=512 count=1 conv=notrunc
	dd if=$(build)/boot/loader.bin of=$(build)/master.img bs=512 count=4 seek=2 conv=notrunc
	dd if=$(build)/system.bin of=$(build)/master.img bs=512 count=200 seek=10 conv=notrunc


.PHONY: clean
clean :
	rm -r $(build)
	

.PHONY : bochs
bochs :$(build)/master.img
	bochs -q



```
# 修改bochsrc文件里的路径
```
ata0-master: type=disk,path="../build/master.img",mode=flat#这里
```


